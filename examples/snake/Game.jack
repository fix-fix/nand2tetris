class Game {
  static int FPS, FRAMES_PER_TICK;
  static String msg_game_over;

  field int tick;
  field Snake snake;
  field UI ui;
  field int GAME_STATE;

  constructor Game new() {
    let FPS = 30;
    let FRAMES_PER_TICK = FPS / 15;
    let msg_game_over = "=== Game Over! ===";
    let GAME_STATE = 0;
    let ui = UI.new();
    return this;
  }

  method void start() {
    if (~(snake = null)) {
      do snake.dispose();
    }
    let GAME_STATE = 1;

    while (~(GAME_STATE = 0)) {
      do Screen.clearScreen();
      do ui.init();
      let tick = 0;
      let snake = Snake.new(30, 20);
      do run();
    }
    return;
  }

  /** Disposes this game. */
  method void dispose() {
    do snake.dispose();
    do ui.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method int run() {
    var char key;  // the key currently pressed by the user
    var boolean exit;
    var boolean is_over;
    var int direction;
    let exit = false;
    let is_over = false;

    do snake.drawFull();

    while (~exit) {
      let key = Keyboard.keyPressed();

      if (key = 81)  { let exit = true; let GAME_STATE = 0; }     // q key
      if (key = 131) { let direction = 1; }   // up arrow
      if (key = 133) { let direction = 2; }   // down arrow
      if (key = 130) { let direction = 3; }   // left arrow
      if (key = 132) { let direction = 4; }   // right arrow

      if (~(direction = 0)) {
        do snake.setDirection(direction);
      }

      let is_over = tickGame();
      let exit = exit | is_over;
      if (is_over) {
        do endGameLoop();
      }
    } // while
    return GAME_STATE;
  }

  method boolean tickGame() {
    var boolean is_over;
    let tick = tick + 1;
    if ((Utils.modulo(tick, FRAMES_PER_TICK) = 0)) {
      let is_over = snake.advance();
    }
		do Sys.wait(1000 / FPS);
    return is_over;
  }

  method void endGameLoop() {
    var char key;
    let GAME_STATE = 2;
    do UI.drawPanel(200, 100, msg_game_over);
    while (GAME_STATE = 2) {
      let key = Keyboard.keyPressed();
      if (key = 82)  { let GAME_STATE = 1; }     // R key
      do Sys.wait(1000 / FPS);
    }
    return;
  }
}

