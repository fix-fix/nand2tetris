class Game {
  static int FPS, FRAMES_PER_TICK;
  static String msg_game_over;
  static int some_prime_num;

  field int key, tick, game_counter, GAME_STATE, apple_coords, score;
  field Snake snake;
  field UI ui;

  constructor Game new() {
    let FPS = 60;
    let FRAMES_PER_TICK = FPS / 30;
    let msg_game_over = "=== Game Over! ===";
    let GAME_STATE = 0;
    let score = 0;
    let ui = UI.new();
    let apple_coords = 42;
    let game_counter = 0;
    let some_prime_num = 2341;
    return this;
  }

  method void start() {
    if (~(snake = null)) {
      do snake.dispose();
    }
    let GAME_STATE = 1;

    while (~(GAME_STATE = 0)) {
      do Screen.clearScreen();
      do ui.init();
      let tick = 0;
      let score = 0;
      let snake = Snake.new(30, 20);
      let apple_coords = placeApple(snake);
      do UI.drawIngameMenu(score);
      do run();
    }
    return;
  }

  /** Disposes this game. */
  method void dispose() {
    do snake.dispose();
    do ui.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method int run() {
    var char tempKey;  // the key currently pressed by the user
    var boolean exit;
    var boolean is_over;
    let exit = false;
    let is_over = false;

    do snake.drawFull();

    while (~exit) {
      let tempKey = Keyboard.keyPressed();
      if (~(tempKey = 0)) {
        let key = tempKey;
      }
      if (key = 81)  { let exit = true; let GAME_STATE = 0; return GAME_STATE; }     // q key

      let is_over = tickGame();
      do UI.updateIngameMenu(score);

      let exit = exit | is_over;
      if (is_over) {
        do endGameLoop();
      }
    } // while
    return GAME_STATE;
  }

  method boolean tickGame() {
    var boolean is_over, is_eaten;
    var int direction;
    var Array result;
    let tick = tick + 1;
    if (Utils.modulo(tick, FRAMES_PER_TICK) = 0) {
      let game_counter = game_counter + 1;

      if (key = 131) { let direction = 1; }   // up arrow
      else {
      if (key = 133) { let direction = 2; }   // down arrow
      else {
      if (key = 130) { let direction = 3; }   // left arrow
      else {
      if (key = 132) { let direction = 4; }   // right arrow
      }
      }
      }

      if (~(direction = 0)) {
        do snake.setDirection(direction);
      }

      let result = snake.advance(apple_coords);
      let is_over = result[0];
      let is_eaten = result[1];
      if (is_eaten) {
        let score = score + 1;
      }
      let apple_coords = placeApple(snake);
      do drawApple();
    }
		do Sys.wait(1000 / FPS);
    let key = Keyboard.keyPressed();
    return is_over;
  }

  method void endGameLoop() {
    var char key;
    let GAME_STATE = 2;
    do UI.drawPanel(200, 100, msg_game_over);
    do snake.dispose();
    while (GAME_STATE = 2) {
      let key = Keyboard.keyPressed();
      if (key = 82)  { let GAME_STATE = 1; }     // R key
      do Sys.wait(1000 / FPS);
    }
    return;
  }

  method int placeApple(Snake snake) {
    var int oldPlace;
    if (~(snake.getHead() = apple_coords)) {
      return apple_coords;
    }
    let oldPlace = apple_coords;
    let oldPlace = Utils.modulo(oldPlace + some_prime_num, UI.getBoardIndexSize() - 1);
    while (snake.isCollided(oldPlace)) {
      let oldPlace = Utils.modulo(oldPlace + some_prime_num, UI.getBoardIndexSize() - 1);
    }
    return oldPlace;
  }

  method void drawApple() {
    do UI.drawBlockIndex(true, apple_coords);
    return;
  }
}

