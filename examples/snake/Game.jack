class Game {
  static int FPS, FRAMES_PER_TICK;
  static String msg_game_over;

  field int tick, game_counter, GAME_STATE, apple_coords;
  field Snake snake;
  field UI ui;

  constructor Game new() {
    let FPS = 60;
    let FRAMES_PER_TICK = FPS / 10;
    let msg_game_over = "=== Game Over! ===";
    let GAME_STATE = 0;
    let ui = UI.new();
    let apple_coords = 42;
    let game_counter = 0;
    return this;
  }

  method void start() {
    if (~(snake = null)) {
      do snake.dispose();
    }
    let GAME_STATE = 1;

    while (~(GAME_STATE = 0)) {
      do Screen.clearScreen();
      do ui.init();
      let tick = 0;
      let snake = Snake.new(30, 20);
      let apple_coords = placeApple(snake);
      do run();
    }
    return;
  }

  /** Disposes this game. */
  method void dispose() {
    do snake.dispose();
    do ui.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method int run() {
    var char key;  // the key currently pressed by the user
    var boolean exit;
    var boolean is_over;
    let exit = false;
    let is_over = false;

    do snake.drawFull();

    while (~exit) {
      let key = Keyboard.keyPressed();
      if (key = 81)  { let exit = true; let GAME_STATE = 0; return GAME_STATE; }     // q key

      let is_over = tickGame();
      let exit = exit | is_over;
      if (is_over) {
        do endGameLoop();
      }
    } // while
    return GAME_STATE;
  }

  method boolean tickGame() {
    var char key;  // the key currently pressed by the user
    var boolean is_over;
    var int direction;
    let tick = tick + 1;
    if ((Utils.modulo(tick, FRAMES_PER_TICK) = 0)) {
      let game_counter = game_counter + 1;

      let key = Keyboard.keyPressed();

      if (key = 131) { let direction = 1; }   // up arrow
      if (key = 133) { let direction = 2; }   // down arrow
      if (key = 130) { let direction = 3; }   // left arrow
      if (key = 132) { let direction = 4; }   // right arrow

      if (~(direction = 0)) {
        do snake.setDirection(direction);
      }

      let is_over = snake.advance(apple_coords);
      let apple_coords = placeApple(snake);
      do drawApple();
    }
		do Sys.wait(1000 / FPS);
    return is_over;
  }

  method void endGameLoop() {
    var char key;
    let GAME_STATE = 2;
    do UI.drawPanel(200, 100, msg_game_over);
    do snake.dispose();
    while (GAME_STATE = 2) {
      let key = Keyboard.keyPressed();
      if (key = 82)  { let GAME_STATE = 1; }     // R key
      do Sys.wait(1000 / FPS);
    }
    return;
  }

  method int placeApple(Snake snake) {
    var int oldPlace;
    if (~(snake.getHead() = apple_coords)) {
      return apple_coords;
    }
    let oldPlace = apple_coords;
    let oldPlace = Utils.modulo(oldPlace + 2341, UI.getBoardIndexSize() - 1);
    while (snake.isCollided(oldPlace)) {
      let oldPlace = Utils.modulo(oldPlace + 2341, UI.getBoardIndexSize() - 1);
    }
    return oldPlace;
  }

  method void drawApple() {
    do UI.drawBlockIndex(true, apple_coords);
    return;
  }
}

